group 'de.tuberlin.mcc'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.3.31'
    ext.ktorVersion = '1.2.1'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}


apply plugin: 'java'
apply plugin: 'kotlin'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile("ch.qos.logback:logback-classic:1.2.3")
    compile group: 'com.google.code.gson', name: 'gson', version: '2.3.1'
    compile "io.ktor:ktor-server-core:$ktorVersion"
    compile "io.ktor:ktor-server-netty:$ktorVersion"
    compile "io.ktor:ktor-client-core:$ktorVersion"
    compile "io.ktor:ktor-client-core-jvm:$ktorVersion"
    compile "io.ktor:ktor-client-apache:$ktorVersion"
    compile "com.xenomachina:kotlin-argparser:2.0.7"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
jar {
    manifest {
        attributes 'Main-Class': 'MainKt'
    }

    from {
        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

void jarFactory(Jar jarTask, jarName, mainClass) {

    jarTask.manifest {
        attributes(
                'Main-Class':  mainClass
        )
    }
    jarTask.zip64 = true
    jarTask.classifier = 'all'
    jarTask.baseName = jarName
    jarTask.from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    jarTask.with jar
    jarTask.doLast {
        println 'Build ' + jarTask.name + ' ended'
    }
}

task buildMatchingTool(type: Jar) {
    jarFactory(it, 'matchingTool', 'tools.matchingtool.MatchingTool')
}

task buildWorkloadGenerator(type: Jar) {
    jarFactory(it, 'wlgenerator', 'tools.wlgenerator.GeneratorTool')
}

task buildRunTool(type: Jar) {
    jarFactory(it, 'runner', 'tools.runner.RunTool')
}

task buildRequestDistributionTool(type: Jar) {
    jarFactory(it, 'reqdistribution', 'tools.requestdistribution.RequestDistributionTool')
}

task buildBoxPlotTool(type: Jar) {
    jarFactory(it, 'boxplot', 'tools.evaluation.BoxPlot')
}